// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        kotlin_version = '1.3.41'
        kotlin_serialization_version = '0.11.0'
        coroutines_version = "1.2.1"

        android_tools_version = '3.4.2'

        build_tools_version = "28.0.3"

        min_sdk_version = 19
        compile_sdk_version = 28
        target_sdk_version = compile_sdk_version

        support_lib_version = "28.0.0"
        play_services_version = "15.0.0"
        firebase_iid_version = "17.1.2"

        test_runner_version = "1.0.2"
        test_rules_version = test_runner_version

        espresso_version = "3.0.2"
        junit_version = "4.12"
        mockito_version = "2.23.0"
        mockito_kotlin_version = "2.0.0"
        robolectric_version = "4.1"
        mockk_version = "1.9.2"
        assertk_version = "0.14"
        detekt_version = "1.0.0-RC14"

        moshi_version = "1.8.0"
        okhttp_version = "3.14.1"

        bivrost_version = "v0.7.1"
        kmnid_version = "0.3.2"
        kethereum_version = "0.75.1"
        tweetnacl_k_version = "0.0.1"
        did_jwt_version = "36f91a7562"//"0.3.0"
        kotlin_common_version = "0.1.1"
        uport_signer_version = "0.3.0"

        uport_sdk_version = "v0.4.2"
    }

    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_tools_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.github.gnosis.bivrost-kotlin:bivrost-gradle-plugin:$bivrost_version"

        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"

        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
}

plugins {
    id "io.errorlab.gradle.vault" version "0.1.0"
}

apply plugin: "io.gitlab.arturbosch.detekt"

detekt {
    version = detekt_version
    input = files(
            "$projectDir"
    )
    //config = "${projectDir}/detekt.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*,.*/build/.*"
    parallel = true
    reports {
        xml {
            enabled = true
            destination = file("${project.buildDir}/reports/detekt.xml")
        }
        html {
            enabled = true
            destination = file("${project.buildDir}/reports/detekt.html")
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://kotlin.bintray.com/kotlinx" }
//        mavenLocal()
    }

//address warnings about multiple kotlin runtimes in classpath
    configurations.all {

        resolutionStrategy.dependencySubstitution {
            substitute module("org.jetbrains.kotlin:kotlin-stdlib-jre7") because "warning about multiple runtimes in the classpath" with module("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
            substitute module("org.jetbrains.kotlin:kotlin-stdlib-jre8") because "warning about multiple runtimes in the classpath" with module("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")

        }
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'org.jetbrains.kotlin' && requested.name in [
                    'kotlin-reflect', 'kotlin-stdlib', 'kotlin-stdlib-common',
                    'kotlin-stdlib-jdk7', 'kotlin-stdlib-jdk8',
            ]) {
                details.useVersion kotlin_version
            }
        }

        //XXX: this is needed until https://github.com/komputing/KEthereum/issues/65 is fixed
        exclude group: "com.github.walleth"
    }
}

subprojects { subproject ->

    afterEvaluate {

        if (subproject.plugins.hasPlugin("com.android.application") || subproject.plugins.hasPlugin("com.android.library")) {
            subproject.android {
                packagingOptions {
                    exclude "META-INF/main.kotlin_module"
                    exclude "META-INF/atomicfu.kotlin_module"
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
            }
        }

        subproject.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }
}

apply from: "publishing.gradle"

task clean(type: Delete) {
    delete rootProject.buildDir
}
